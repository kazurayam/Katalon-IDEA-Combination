plugins {
    id("com.katalon.gradle-plugin") version "0.1.2"
}

// run "$ gradle :katalon:katalonCopyDependencies" command to download
// the monk-directory-scanner-0.1.1.jar into the "Drivers" folder

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.kazurayam:monk-directory-scanner:0.1.1")
}

ext {
    LibVersion = '0.1.0'
}

tasks.register('importMonkDirectoryScanner') {
    group = 'Custom'
    description = 'import the MonkDirectoryScanner into the Drivers folder'
    doFirst {
        delete fileTree("Drivers").matching {
            include "*monk-directory-scanner-*.jar"
        }
    }
}
importMonkDirectoryScanner.finalizedBy katalonCopyDependencies

tasks.register('importLibArtifact') {
    group = 'Custom'
    description = "import the KS_ObjectRepositoryGarbageCollector jar and the MockDirectoryScanner jar into the Drivers folder"
    doFirst {
        delete fileTree("Drivers").matching {
            include "my-ks-helper-*.jar"
        }
    }
    doLast {
        copy {
            into layout.projectDirectory.dir("Drivers")
            from("${rootProject.projectDir}/lib/build/libs") {
                include "my-ks-helper-${LibVersion}*.jar"
            }
        }
    }
}

tasks.register('importDrivers') {
    group = 'Custom'
    description = 'run importLibArtifact and importMonDirectoryScanner'
    dependsOn importLibArtifact, importMonkDirectoryScanner
}

tasks.register('cleanProject', Delete) {
    group = 'Custom'
    description = 'remove the bin folder and others to clean up the Katalo project'
    delete 'bin'
    delete 'Libs'
    delete '.cache'
}



